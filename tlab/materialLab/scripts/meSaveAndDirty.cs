//==============================================================================
// TorqueLab ->
// Copyright (c) 2015 All Right Reserved, http://nordiklab.com/
//------------------------------------------------------------------------------
//==============================================================================
//==============================================================================
// Material Update Functionality
$Pref::MaterialLabGui::DefaultMaterialFile = "art/materials.cs";
function MaterialLabGui::saveDialogCancel( %this ) {
	MaterialLabGui.guiSync( materialLab_previewMaterial );
}
//------------------------------------------------------------------------------
//==============================================================================
function MaterialLabGui::saveDialogDontSave( %this, %material ) {
	MaterialLabGui.currentMaterial.setName( %this.originalName );
	//restore to defaults
	MaterialLabGui.copyMaterials( notDirtyMaterial, MaterialLabGui.currentMaterial );
	MaterialLabGui.copyMaterials( notDirtyMaterial, materialLab_previewMaterial );
	MaterialLabGui.guiSync( materialLab_previewMaterial );
	materialLab_previewMaterial.flush();
	materialLab_previewMaterial.reload();
	MaterialLabGui.currentMaterial.flush();
	MaterialLabGui.currentMaterial.reload();
	MaterialLabGui.setMaterialNotDirty();
	MaterialLabGui.setActiveMaterial( %material );
}
//------------------------------------------------------------------------------
//==============================================================================
function MaterialLabGui::saveDialogSave( %this, %material ) {
	MaterialLabGui.save();
	MaterialLabGui.setActiveMaterial( %material );
}
//------------------------------------------------------------------------------
//==============================================================================
function MaterialLabGui::save( %this ) {
	if( MaterialLabGui.currentMaterial.getName() $= "" ) {
		LabMsgOK("Cannot perform operation", "Saved materials cannot be named \"\". A name must be given before operation is performed" );
		return;
	}

	

	// Update the live object regardless in this case
	MaterialLabGui.updateLivePreview(true);
	%currentMaterial = MaterialLabGui.currentMaterial;

	if( %currentMaterial == -1 ) {
		LabMsgOK("Cannot perform operation", "Could not locate material" );
		return;
	}

	// Specifically for materials autogenerated from shapes.
	if( %currentMaterial.isAutoGenerated() )
		%currentMaterial.setAutoGenerated( false );

	// Save the material using the persistence manager
	matLab_PersistMan.saveDirty();
	// Clean up the Material Editor
	MaterialLabGui.copyMaterials( materialLab_previewMaterial, notDirtyMaterial );
	MaterialLabGui.setMaterialNotDirty();
}
//------------------------------------------------------------------------------

//==============================================================================
function MaterialLabGui::setMaterialNotDirty(%this) {
	%propertyText = "Material Properties";
	%previewText = "Material Preview";
	MaterialLabPropertiesWindow.text = %propertyText;
	MaterialLabPreviewWindow.text = %previewText;
	MaterialLabPropertiesWindow-->quickSaveButton.active = 0;
	MaterialLabGui.materialDirty = false;
	matLab_PersistMan.removeDirty(MaterialLabGui.currentMaterial);
}
//------------------------------------------------------------------------------
//==============================================================================
function MaterialLabGui::setMaterialDirty(%this) {
	%propertyText = "Material Properties *";
	%previewText = "Material Preview *";
	MaterialLabPropertiesWindow.text = %propertyText;
	MaterialLabPreviewWindow.text = %previewText;
	MaterialLabGui.materialDirty = true;
	MaterialLabPropertiesWindow-->quickSaveButton.active = 1;

	// materials created in the material selector are given that as its filename, so we run another check
	if( MaterialLabGui.isMatLabitorMaterial( MaterialLabGui.currentMaterial ) ) {
		if( MaterialLabGui.currentMaterial.isAutoGenerated() ) {
			%obj = MaterialLabGui.currentObject;

			if( %obj.shapeName !$= "" )
				%shapePath = %obj.shapeName;
			else if( %obj.isMethod("getDatablock") ) {
				if( %obj.getDatablock().shapeFile !$= "" )
					%shapePath = %obj.getDatablock().shapeFile;
			}

			//creating toPath
			%k = 0;

			while( strpos( %shapePath, "/", %k ) != -1 ) {
				%pos = strpos( %shapePath, "/", %k );
				%k = %pos + 1;
			}

			%savePath = getSubStr( %shapePath , 0 , %k );
			%savePath = %savePath @ "materials.cs";
			matLab_PersistMan.setDirty(MaterialLabGui.currentMaterial, %savePath);
		} else {
			matLab_PersistMan.setDirty(MaterialLabGui.currentMaterial, "art/materials.cs");
		}
	} else
		matLab_PersistMan.setDirty(MaterialLabGui.currentMaterial);
}
//------------------------------------------------------------------------------

//==============================================================================
function MaterialLabGui::showSaveDialog( %this, %toMaterial ) {
	LabMsgYesNoCancel("Save Material?",
							"The material " @ MaterialLabGui.currentMaterial.getName() @ " has unsaved changes. <br>Do you want to save?",
							"MaterialLabGui.saveDialogSave(" @ %toMaterial @ ");",
							"MaterialLabGui.saveDialogDontSave(" @ %toMaterial @ ");",
							"MaterialLabGui.saveDialogCancel();" );
}
//------------------------------------------------------------------------------
//==============================================================================
function MaterialLabGui::showMaterialChangeSaveDialog( %this, %toMaterial ) {
	%fromMaterial = MaterialLabGui.currentMaterial;
	LabMsgYesNoCancel("Save Material?",
							"The material " @ %fromMaterial.getName() @ " has unsaved changes. <br>Do you want to save before changing the material?",
							"MaterialLabGui.saveDialogSave(" @ %toMaterial @ "); MaterialLabGui.changeMaterial(" @ %fromMaterial @ ", " @ %toMaterial @ ");",
							"MaterialLabGui.saveDialogDontSave(" @ %toMaterial @ "); MaterialLabGui.changeMaterial(" @ %fromMaterial @ ", " @ %toMaterial @ ");",
							"MaterialLabGui.saveDialogCancel();" );
}
//------------------------------------------------------------------------------