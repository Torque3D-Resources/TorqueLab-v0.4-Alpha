//==============================================================================
// TorqueLab ->
// Copyright (c) 2015 All Right Reserved, http://nordiklab.com/
//------------------------------------------------------------------------------
//==============================================================================
//==============================================================================
// Material Update Functionality
$Pref::MatLab::DefaultMaterialFile = "art/materials.cs";
function MatLab::saveDialogCancel( %this ) {
	MatLab.guiSync( materialLab_previewMaterial );
}
//------------------------------------------------------------------------------
//==============================================================================
function MatLab::saveDialogDontSave( %this, %material ) {
	MatLab.currentMaterial.setName( %this.originalName );
	//restore to defaults
	MatLab.copyMaterials( notDirtyMaterialLab, MatLab.currentMaterial );
	MatLab.copyMaterials( notDirtyMaterialLab, materialLab_previewMaterial );
	MatLab.guiSync( materialLab_previewMaterial );
	materialLab_previewMaterial.flush();
	materialLab_previewMaterial.reload();
	MatLab.currentMaterial.flush();
	MatLab.currentMaterial.reload();
	MatLab.setMaterialNotDirty();
	MatLab.setActiveMaterial( %material );
}
//------------------------------------------------------------------------------
//==============================================================================
function MatLab::saveDialogSave( %this, %material ) {
	MatLab.save();
	MatLab.setActiveMaterial( %material );
}
//------------------------------------------------------------------------------
//==============================================================================
function MatLab::save( %this ) {
	if( MatLab.currentMaterial.getName() $= "" ) {
		LabMsgOK("Cannot perform operation", "Saved materials cannot be named \"\". A name must be given before operation is performed" );
		return;
	}

	

	// Update the live object regardless in this case
	MatLab.updateLivePreview(true);
	%currentMaterial = MatLab.currentMaterial;

	if( %currentMaterial == -1 ) {
		LabMsgOK("Cannot perform operation", "Could not locate material" );
		return;
	}

	// Specifically for materials autogenerated from shapes.
	if( %currentMaterial.isAutoGenerated() )
		%currentMaterial.setAutoGenerated( false );

	// Save the material using the persistence manager
	MatLab_PM.saveDirty();
	// Clean up the Material Editor
	MatLab.copyMaterials( materialLab_previewMaterial, notDirtyMaterialLab );
	MatLab.setMaterialNotDirty();
}
//------------------------------------------------------------------------------

//==============================================================================
function MatLab::setMaterialNotDirty(%this) {
	%propertyText = "Material Properties";
	%previewText = "Material Preview";
	MaterialLabPropertiesWindow.text = %propertyText;
	MaterialLabPreviewWindow.text = %previewText;
	MaterialLabPropertiesWindow-->quickSaveButton.active = 0;
	MatLab.materialDirty = false;
	MatLab_PM.removeDirty(MatLab.currentMaterial);
}
//------------------------------------------------------------------------------
//==============================================================================
function MatLab::setMaterialDirty(%this) {
	%propertyText = "Material Properties *";
	%previewText = "Material Preview *";
	MaterialLabPropertiesWindow.text = %propertyText;
	MaterialLabPreviewWindow.text = %previewText;
	MatLab.materialDirty = true;
	MaterialLabPropertiesWindow-->quickSaveButton.active = 1;

	// materials created in the material selector are given that as its filename, so we run another check
	if( MatLab.isMatLabitorMaterial( MatLab.currentMaterial ) ) {
		if( MatLab.currentMaterial.isAutoGenerated() ) {
			%obj = MatLab.currentObject;

			if( %obj.shapeName !$= "" )
				%shapePath = %obj.shapeName;
			else if( %obj.isMethod("getDatablock") ) {
				if( %obj.getDatablock().shapeFile !$= "" )
					%shapePath = %obj.getDatablock().shapeFile;
			}

			//creating toPath
			%k = 0;

			while( strpos( %shapePath, "/", %k ) != -1 ) {
				%pos = strpos( %shapePath, "/", %k );
				%k = %pos + 1;
			}

			%savePath = getSubStr( %shapePath , 0 , %k );
			%savePath = %savePath @ "materials.cs";
			MatLab_PM.setDirty(MatLab.currentMaterial, %savePath);
		} else {
			MatLab_PM.setDirty(MatLab.currentMaterial, "art/materials.cs");
		}
	} else
		MatLab_PM.setDirty(MatLab.currentMaterial);
}
//------------------------------------------------------------------------------

//==============================================================================
function MatLab::showSaveDialog( %this, %toMaterial ) {
	LabMsgYesNoCancel("Save Material?",
							"The material " @ MatLab.currentMaterial.getName() @ " has unsaved changes. <br>Do you want to save?",
							"MatLab.saveDialogSave(" @ %toMaterial @ ");",
							"MatLab.saveDialogDontSave(" @ %toMaterial @ ");",
							"MatLab.saveDialogCancel();" );
}
//------------------------------------------------------------------------------
//==============================================================================
function MatLab::showMaterialChangeSaveDialog( %this, %toMaterial ) {
	%fromMaterial = MatLab.currentMaterial;
	LabMsgYesNoCancel("Save Material?",
							"The material " @ %fromMaterial.getName() @ " has unsaved changes. <br>Do you want to save before changing the material?",
							"MatLab.saveDialogSave(" @ %toMaterial @ "); MatLab.changeMaterial(" @ %fromMaterial @ ", " @ %toMaterial @ ");",
							"MatLab.saveDialogDontSave(" @ %toMaterial @ "); MatLab.changeMaterial(" @ %fromMaterial @ ", " @ %toMaterial @ ");",
							"MatLab.saveDialogCancel();" );
}
//------------------------------------------------------------------------------